import sys
import os
import string
import SCons.Errors


Default('build')


# Courtesy of Ben Scott
def ParseConfig(env, command, options):
    "Parses xxx-config style output for compilation directives"

    # Run the command
    where = WhereIs(command)
    if not where:
        print "Looked for " + str(command) + " but could not find it in PATH"
        raise SCons.Errors.UserError, ('%s not found in PATH' % command)
    print "Found " + str(command) + ": " + where
    cmd = where + ' ' + string.join(options)
    params = string.split(os.popen(cmd).read())

    # Parse its output
    for arg in params:
        switch = arg[0:1]
        option = arg[1:2]
        if switch == '-':
            if option == 'I':
                env.Append(CPPPATH = [arg[2:]])
            elif option == 'L':
                env.Append(LIBPATH = [arg[2:]])
            elif option == 'l':
                env.Append(LIBS = [arg[2:]])
            elif arg[0:11] == '-Wl,-rpath,':
                env.Append(LINKFLAGS = [arg])
            else:
                env.Append(CCFLAGS = [arg])
        else:
            # Must be a static library, add it to the libs
            # XXX think about this more...
            env.Append(LIBS = [arg])


def SetupCal3D(env):
    env.Append(LIBS = ['cal3d', 'm'])

def SetupCorona(env):
    try:
        ParseConfig(env, 'corona-config', ['--cxxflags', '--libs'])
    except SCons.Errors.UserError, e:
        env.Append(LIBS = ['corona', 'jpeg', 'png', 'z'])

def SetupExpat(env):
    env.Append(LIBS = ['expat'])

def SetupGLText(env):
    env.Append(LIBS = ['gltext'])

def SetupOpenGL(env):
    if env['PLATFORM'] == 'cygwin':
        env.Append(LIBS = ['glu32', 'opengl32'])
    else:
        env.Append(LIBS = ['GLU', 'GL'])

def SetupPHUI(env):
    env.Append(CPPPATH = ['#/src'],
               LIBPATH = ['#/src/phui'],
               LIBS = ['phui'])

def SetupPyrBase(env):
    env.Append(CPPPATH = ['#/src/base'],
               LIBPATH = ['#/src/base'],
               LIBS = ['base'])

def SetupNSPR(env):
    try:
        # Try using nspr-config first.
        ParseConfig(env, 'nspr-config', ['--cflags', '--libs'])
    except SCons.Errors.UserError, e:
        # @todo  make it only include /usr/mozilla...  on debian
        #        check uname's output?
        env.Append(
            CPPPATH = ['/usr/include/mozilla/nspr', '/usr/include/nspr'],
            LIBS = ['nspr4', 'pthread'])

def SetupSDL(env):
    if env['PLATFORM'] == 'cygwin':
        env.Append(CPPPATH = '/usr/local/include/SDL',
                   LIBS = ['SDL', 'SDLmain'])
    else:
        ParseConfig(env, 'sdl-config', ['--cflags', '--libs'])

def SetupWxWindows(env):
    args = ['--cxxflags', '--libs']
    if env['PLATFORM'] != 'cygwin':
        args.append('--gl-libs')  # special case cygwin's inconsistency
    ParseConfig(env, 'wx-config', args)


# Initialize set of external dependencies.
TOOLS = {
    'Cal3D'     : SetupCal3D,
    'Corona'    : SetupCorona,
    'Expat'     : SetupExpat,
    'GLText'    : SetupGLText,
    'OpenGL'    : SetupOpenGL,
    'PHUI'      : SetupPHUI,
    'PyrBase'   : SetupPyrBase,
    'NSPR'      : SetupNSPR,
    'SDL'       : SetupSDL,
    'wxWindows' : SetupWxWindows }
Export('TOOLS')


# Create base environment.
base_env = Environment(ENV = os.environ)

if base_env['PLATFORM'] == 'cygwin':
    base_env.Append(CCFLAGS = ['-DWIN32', '-D_WIN32', '-DCYGWIN'],
                    CPPPATH = ['#/third-party-cygwin/include'],
                    LIBPATH = ['#/third-party-cygwin/lib'])

if base_env['PLATFORM'] in ['cygwin', 'irix']:
    base_env['USE_EXTGL'] = 1
    base_env.Append(CCFLAGS = ['-DPYR_USE_EXTGL'])
else:
    base_env['USE_EXTGL'] = 0

if base_env['PLATFORM'] == 'irix':
    base_env.Append(
        CPPPATH = string.split(os.environ['CPLUS_INCLUDE_PATH'], ':'),
        LIBPATH = string.split(os.environ['LIBRARY_PATH'], ':'),
        LIBS = ['m'])

# Process build options.
if ARGUMENTS.get('dbg'):
    base_env.Append(CCFLAGS = ['-g', '-DDEBUG'])
if ARGUMENTS.get('opt'):
    base_env.Append(CCFLAGS = ['-O2'],
                    LINKFLAGS = ['-s'])

if ARGUMENTS.get('distcc'):
    base_env['CC']  = ['$(', 'distcc', '$)', base_env['CC']]
    base_env['CXX'] = ['$(', 'distcc', '$)', base_env['CXX']]

Export('base_env')


# Process subdirectories.
SConscript(dirs = ['src'])
